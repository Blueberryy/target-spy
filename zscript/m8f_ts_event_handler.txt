/* Copyright Alexander Kromm (mmaulwurff@gmail.com) 2018
 *
 * This file is part of Target Spy.
 *
 * Target Spy is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Target Spy is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Target Spy.  If not, see <https://www.gnu.org/licenses/>.
 */

class m8f_ts_MultiSettings
{
  Array<m8f_ts_Settings> settingsArray;

  m8f_ts_MultiSettings init()
  {
    for (int i = 0; i < MAXPLAYERS; ++i)
      {
        settingsArray.push(new("m8f_ts_Settings"));
      }

    return self;
  }

  void read(int playerNumber)
  {
    PlayerInfo player = players[playerNumber];
    settingsArray[playerNumber].read(player);
  }

  m8f_ts_Settings get(int playerNumber)
  {
    return settingsArray[playerNumber];
  }

  void maybeUpdate(int playerNumber)
  {
    PlayerInfo player              = players[playerNumber];
    int        optionsUpdatePeriod = CVar.GetCVar("m8f_ts_update_period", player).GetInt();

    if (optionsUpdatePeriod == 0) { read(playerNumber); }

    else if (!settingsArray[playerNumber].initialized)
      {
        settingsArray[playerNumber].initialized = true;
        read(playerNumber);
      }

    else if (optionsUpdatePeriod != -1
             && (level.time % optionsUpdatePeriod) == 0)
      {
        read(playerNumber);
      }
  }
}

class m8f_ts_LastTargetInfo
{
  Actor a;
  int   killTime;
  int   hurtTime;

  m8f_ts_LastTargetInfo init()
  {
    a        = null;
    killTime = -1;
    hurtTime = -1;

    return self;
  }
}

class m8f_ts_MultiLastTargetInfo
{
  Array<m8f_ts_LastTargetInfo> lastTargetInfos;

  m8f_ts_MultiLastTargetInfo init()
  {
    for (int i = 0; i < MAXPLAYERS; ++i)
      {
        lastTargetInfos.push(new("m8f_ts_LastTargetInfo").init());
      }

    return self;
  }

  m8f_ts_LastTargetInfo get(int playerNumber)
  {
    return lastTargetInfos[playerNumber];
  }
}

class m8f_ts_EventHandler : EventHandler
{
  m8f_ts_MultiSettings       multiSettings;
  m8f_ts_MultiLastTargetInfo multiLastTargetInfo;

  m8f_ts_Data     data;
  bool            isTitlemap;
  int             dehackedGameType;

  m8f_ts_TagCache cache;

  override void WorldLoaded(WorldEvent e)
  {
    data               = new("m8f_ts_Data").init();
    isTitlemap         = CheckTitlemap();
    dehackedGameType   = GetDehackedGameType();

    cache              = new("m8f_ts_TagCache").init();
  }

  override void OnRegister()
  {
    multiSettings       = new("m8f_ts_MultiSettings").init();
    multiLastTargetInfo = new("m8f_ts_MultiLastTargetInfo").init();
  }

  override void PlayerEntered(PlayerEvent e)
  {
    multiSettings.read(e.playerNumber);
  }

  override void WorldThingDied(WorldEvent event)
  {
    if (event == null) { return; }

    Actor died = event.thing;
    if (died == null) { return; }

    for (int i = 0; i < MAXPLAYERS; ++i)
      {
        let lastTargetInfo = multiLastTargetInfo.get(i);
        if (lastTargetInfo.a == died)
          {
            lastTargetInfo.killTime = level.time;
          }
      }
  }

  override void WorldThingDamaged(WorldEvent event)
  {
    if (event == null) { return; }

    Actor damagedThing = event.thing;
    if (damagedThing == null) { return; }


    for (int i = 0; i < MAXPLAYERS; ++i)
      {
        let lastTargetInfo = multiLastTargetInfo.get(i);
        if (lastTargetInfo.a == damagedThing)
          {
            lastTargetInfo.hurtTime = level.time;
          }
      }
  }

  override void RenderOverlay(RenderEvent e)
  {
    if (isTitlemap) { return; }

    int        playerNumber = e.camera.PlayerNumber();
    PlayerInfo player       = players[playerNumber];
    multiSettings.maybeUpdate(playerNumber);

    if (automapActive) { return; }

    drawEverything(playerNumber);
  }

  ui void drawEverything(int playerNumber)
  {
    PlayerInfo player = players[playerNumber];
    if (!CVar.GetCVar("m8f_ts_enabled", player).GetInt()) { return; }

    Actor target = GetTarget(playerNumber, 0);

    draw(target, playerNumber);

    if (target != null) { SetLastTarget(target, playerNumber); }
  }

  // Wrapper to access lastTarget in scope play from ui scope.
  // not really const, but lastTarget doesn't affect gameplay.
  void SetLastTarget(Actor newLastTarget, int playerNumber) const
  {
    multiLastTargetInfo.get(playerNumber).a = newLastTarget;
  }

  play bool isSlot1Weapon(int playerNumber) const
  {
    PlayerInfo player = players[playerNumber];
    Weapon w = player.readyWeapon;
    if (w == null) { return true; }

    int located;
    int slot;
    int priority;
    [located, slot, priority] = player.weapons.LocateWeapon(w.GetClassName());
    return (slot == 1);
  }

  ui void drawCrosshairs( Actor target
                        , int   crosshairColor
                        , Font  font
                        , int   playerNumber
                        )
  {
    if (!multiSettings.get(playerNumber).crossOn) { return; }

    if (multiSettings.get(playerNumber).noCrossOnSlot1
        && isSlot1Weapon(playerNumber))
      {
        return;
      }

    if (multiSettings.get(playerNumber).hitConfirmation)
      {
        let lastTargetInfo = multiLastTargetInfo.get(playerNumber);
        if (lastTargetInfo.hurtTime != -1
            && (level.time < lastTargetInfo.hurtTime + 10))
          {
            crosshairColor = multiSettings.get(playerNumber).hitColor;
          }
      }

    double scale = 0.5 / multiSettings.get(playerNumber).crossScale;

    double topY    = 0.48 + multiSettings.get(playerNumber).topOff;
    double centerY = 0.51 + multiSettings.get(playerNumber).crossOff;
    double bottomY = 0.54 + multiSettings.get(playerNumber).botOff;

    int dx = multiSettings.get(playerNumber).xAdjustment;
    drawTextCenter(multiSettings.get(playerNumber).crossTop,  crosshairColor, scale, topY   , font, dx);
    drawTextCenter(multiSettings.get(playerNumber).crosshair, crosshairColor, scale, centerY, font, dx);
    drawTextCenter(multiSettings.get(playerNumber).crossBot,  crosshairColor, scale, bottomY, font, dx);
  }

  ui void draw(Actor target, int playerNumber)
  {
    double y       = multiSettings.get(playerNumber).yStart;
    double newline = 0.03 * multiSettings.get(playerNumber).stepMult;
    if (y >= 0.80) { newline = -newline; }

    Font font = Font.GetFont(multiSettings.get(playerNumber).fontName);

    y = drawKillConfirmed(y, newline, font, playerNumber);

    PlayerInfo player    = players[playerNumber];
    bool       hasTarget = (target != null);
    int        crossCol  = multiSettings.get(playerNumber).crossCol;
    CVar.GetCVar("m8f_ts_has_target", player).SetInt(hasTarget);

    if (!hasTarget)
      {
        drawCrosshairs(target, crossCol, font, playerNumber);
        return;
      }

    let  settings        = multiSettings.get(playerNumber);
    int  targetMaxHealth = GetActorMaxHealth(target);
    bool showHealth      = (targetMaxHealth != 0);
    if (targetMaxHealth < settings.minHealth && targetMaxHealth != 0)
      {
        drawCrosshairs(target, crossCol, font, playerNumber);
        return; // not worth showing
      }

    int targetHealth = target.health;
    if (targetHealth < 1 && !settings.showCorps) // target is dead
      {
        drawCrosshairs(target, crossCol, font, playerNumber);
        return;
      }

    int tagColor;
    if (targetMaxHealth < 100) { tagColor = settings.weakCol; }
    else                       { tagColor = settings.nameCol; }
    int customColor = CustomTargetColor(target);
    if (customColor)           { tagColor = customColor; }

    int percent = 10;
    if (showHealth)
      {
        if (targetHealth > targetMaxHealth) { percent = 11; }
        else { percent = targetHealth * 10 / targetMaxHealth; }
      }

    int targetColor = settings.colors[percent];
    if (targetHealth < 35 && settings.almDeadCr) targetColor = settings.crAlmDead;
    if (targetHealth < 1)                        targetColor = settings.crAlmDead;

    drawCrosshairs(target, targetColor, font, playerNumber);

    double textScale = 0.5 / settings.textScale;

    if (settings.showBar && showHealth)
      {
        string hpBar = MakeHpBar(targetHealth, targetMaxHealth,
                                 settings.logScale,
                                 settings.pipSource, settings.emptyPipSource,
                                 settings.altHpCols,
                                 settings.greenCr, settings.redCr);
        drawTextCenter(hpBar, targetColor, textScale, y, font);
        y += newline;
      }

    int nameColor = tagColor;
    if (targetHealth < 1) { nameColor = targetColor; }

    if (settings.showName)
      {
        string targetName = GetTargetName(target, dehackedGameType, playerNumber);
        if (targetHealth < 1)
          {
            targetName = StringStruct.Format("Remains of %s", targetName);
            nameColor  = targetColor;
          }

        drawTextCenter(targetName, nameColor, textScale, y, font);
        y += newline;
      }

    if (settings.showInfo)
      {
        string targetFlags = GetTargetFlags(target);
        if (targetFlags.Length() > 0)
          {
            drawTextCenter(targetFlags, nameColor, textScale, y, font);
            y += newline;
          }
      }

    if (showHealth)
      {
        switch (settings.showNums)
          {
          case 0: break;

          case 1:
            drawTextCenter(StringStruct.Format("%d/%d",
                                               targetHealth, targetMaxHealth),
                           targetColor, textScale, y, font);
            y += newline;
            break;

          case 2:
            drawTextCenter(StringStruct.Format("%d", targetHealth),
                           targetColor, textScale, y, font);
            y += newline;
            break;

          case 3:
            int percent100;
            if (targetMaxHealth == 0) { percent100 = 100; }
            else { percent100 = targetHealth * 100 / targetMaxHealth; }

            drawTextCenter(StringStruct.Format("%d%", percent100),
                           targetColor, textScale, y, font);
            y += newline;
            break;
          }
      }
  }

  ui double drawKillConfirmed( double y
                             , double newline
                             , Font   font
                             , int    playerNumber
                             )
  {
    if (!multiSettings.get(playerNumber).showKillConfirmation) { return y; }
    let lastTargetInfo = multiLastTargetInfo.get(playerNumber);
    if (lastTargetInfo.killTime == -1) { return y; }
    if (level.time < lastTargetInfo.killTime + 35 * 1)
      {
        double scale = 0.5 / multiSettings.get(playerNumber).textScale;
        drawTextCenter("Kill Confirmed", multiSettings.get(playerNumber).nameCol, scale, y, font);
        y += newline;
      }
    return y;
  }

  ui void drawTextCenter(string text, int color, double scale, double relativeY,
                         Font font, int xAdjustment = 0)
  {
    int width    = scale * Screen.GetWidth();
    int height   = scale * (Screen.GetHeight() - font.GetHeight());
    int position = width - font.StringWidth(text);

    double x = position * 0.5 + xAdjustment;
    double y = height   * relativeY;

    Screen.DrawText(font, color, x, y, text,
                    DTA_KeepRatio,     true,
                    DTA_VirtualWidth,  width,
                    DTA_VirtualHeight, height);
  }

  ui Actor GetTarget(int playerNumber, int gameType)
  {
    PlayerInfo player = players[playerNumber];
    if (!player) { return null; }

    // try an easy way to get a target (also works with autoaim)
    let   translator = new("m8f_ts_PlayToUiTranslator");
    Actor target     = translator.AimTargetWrapper(player.mo);
    let   settings   = multiSettings.get(playerNumber);

    // if target is not found by easy way, try the difficult way
    if (target == null)
      {
        target = translator.LineAttackTargetWrapper(player.mo);
      }

    if (target == null && settings.showObjects > 1)
      {
        target = translator.AimLineAttackWrapper(player.mo);
      }

    // give up
    if (target == null) { return null; }

    string targetClass   = target.GetClassName();
    bool   targetIsSlave = data.slaveActors.contains(targetClass);
    if (targetIsSlave) { return target.Master; }

    bool isInBlackList = data.blackList.contains(targetClass);
    if (isInBlackList) { return null; }

    switch (gameType)
      {
      case 1: targetClass.AppendFormat("_free" ); break; // Freedoom
      case 2: targetClass.AppendFormat("_rekkr"); break; // Rekkr
      }

    // everything without name in REKKR is "blacklisted"
    if (targetClass.IndexOf("_rekkr") != -1)
      {
        string specialName = data.specialNames.get(targetClass);
        if (specialName.length() == 0) { return null; }
      }

    if (target.bISMONSTER)
      {
        if ((target.bSHADOW || target.bSTEALTH) && !settings.showHidden)
          {
            return null;
          }
        if (target.bFRIENDLY && !settings.showFriends)
          {
            return null;
          }
      }
    else // not monsters
      {
        if (target.player) { return target; }

        switch (settings.showObjects)
          {
          case 0: return null;
          case 1:
            if (target.bSHOOTABLE) { return target; }
            else                   { return null;   }
          case 2:
            if (target.bSHOOTABLE || target is "Inventory") { return target; }
            else                                            { return null;   }
          case 3:
            return target;
          }
      }

    return target;
  }

  ui int GetActorMaxHealth(Actor a)
  {
    if (a == null)     { return 0; }
    if (!a.bSHOOTABLE) { return 0; }

    if (a.player && a.player.mo) { return a.player.mo.GetMaxHealth(); }

    int maxHealth = a.SpawnHealth();
    return maxHealth;
  }

  ui int CustomTargetColor(Actor target)
  {
    string customColorTokenClass = "tr_color_token";
    int customColor = target.CountInv(customColorTokenClass);
    return customColor;
  }

  ui static int Log2(int value)
  {
    int result = -1;
    while (value)
      {
        ++result;
        value /= 2;
      }
    return result;
  }

  ui string MakeHpBar(int health, int maxHealth, bool logScale,
                      string pipSource, string emptyPipSource,
                      bool greenRedTheme, int greenColor, int redColor)
  {
    int length;
    if      (logScale)          { length = Log2(maxHealth) * 2; }
    else if (maxHealth >= 2000) { length = 30; }
    else if (maxHealth >= 500)  { length = 20; }
    else                        { length = 10; }

    int pips = health * length / maxHealth;
    if      (pips == 0)      { pips = 1; }
    else if (pips >  length) { pips = length; }

    string pipColor;
    string emptyColor;
    if (greenRedTheme)
      {
        pipColor   = StringStruct.Format("\c%c", 97 + greenColor); // 'a'
        emptyColor = StringStruct.Format("\c%c", 97 + redColor);   // 'a'
      }
    else
      {
        pipColor   = "";
        emptyColor = "";
      }

    return StringStruct.Format("%s%s%s%s",
                               pipColor,   pipSource.Left(pips),
                               emptyColor, emptyPipSource.Left(length - pips));

  }

  bool CheckTitlemap()
  {
    bool isTitlemap = (level.mapname == "TITLEMAP");
    return isTitlemap;
  }

  int GetDehackedGameType()
  {
    string checkString = StringTable.Localize("$E1TEXT");
    string rekkrString = "You've been through war.";
    bool   isRekkr     = (checkString.IndexOf(rekkrString) >= 0);
    if (isRekkr) { return 2; }

    string impName     = StringTable.Localize("$CC_IMP");
    bool   isFreedoom  = (impName == "serpentipede");
    if (isFreedoom) { return 1; }

    return 0;
  }

  ui string GetTargetName(Actor target, int gameType, int playerNumber)
  {
    if (target.player) { return target.player.GetUserName(); }

    string targetClass = target.GetClassName();

    switch (multiSettings.get(playerNumber).showInternalNames)
      {
      case 0: break;
      case 1: return targetClass;
      case 2: return Beautify(targetClass);
      }

    switch (gameType)
      {
      case 1: targetClass.AppendFormat("_free" ); break; // Freedoom
      case 2: targetClass.AppendFormat("_rekkr"); break; // Rekkr
      }

    // if target name was found before, just return it
    if (cache.cachedClass == targetClass)
      {
        return AddAdditionalInfo(target, cache.cachedTag, playerNumber);
      }

    // if target name is set via actor tag, return it
    string targetName = target.GetTag();
    if (targetName != targetClass && !(gameType != 0))
      {
        cache.SetCache(targetClass, targetName);
        return AddAdditionalInfo(target, targetName, playerNumber);
      }

    string specialName = data.specialNames.get(targetClass);
    if (specialName.Length() != 0)
      {
        targetName = specialName;
        cache.SetCache(targetClass, targetName);
        return AddAdditionalInfo(target, targetName, playerNumber);
      }

    // if target name is not found, compose tag from class name
    targetName = Beautify(targetName);

    cache.SetCache(targetClass, targetName);
    return AddAdditionalInfo(target, targetName, playerNumber);
  }

  ui string Beautify(string s)
  {
    string result = SeparateCamelCase(s);
    result.Replace("_", " ");
    return result;
  }

  ui string AddAdditionalInfo(Actor target, string name, int playerNumber)
  {
    Inventory inv = Inventory(target);
    if (inv)
      {
        int amount = inv.amount;
        if (amount == 1)
          {
            BasicArmorPickup armor = BasicArmorPickup(inv);
            if (armor) { amount = armor.SaveAmount; }
          }
        if (amount == 1) { return name; }
        else             { return StringStruct.Format("%s (%i)", name, amount); }
      }
    else
      {
        return PrependChampionColor(target, name, playerNumber);
      }
  }

  ui string PrependChampionColor(Actor target, string name, int playerNumber)
  {
    if (!multiSettings.get(playerNumber).showChampion) { return name; }

    string tokenClass = "champion_Token";
    Inventory token = target.FindInventory(tokenClass, true);
    if (!token) { return name; }

    string tokenClassName = token.GetClassName();
    string championTag    = data.championTokens.get(tokenClassName);
    if (championTag.Length() == 0) { championTag = "Champion"; }

    championTag.AppendFormat(" %s", name);
    return championTag;
  }

  ui static string SeparateCamelCase(string source)
  {
    int sourceLength = source.Length();
    string result = "";
    string letter1 = source.CharAt(0);
    string letter2;

    for (int i = 1; i < sourceLength; ++i)
      {
        letter2 = source.CharAt(i);
        if (IsSmallLetter(letter1) && IsBigLetter(letter2))
          {
            result.AppendFormat("%s ", letter1);
          }
        else
          {
            result.AppendFormat(letter1);
          }
        letter1 = letter2;
      }
    result.AppendFormat(letter2);

    return result;
  }

  ui static bool IsSmallLetter(string letter)
  {
    int code = letter.CharCodeAt(0);
    return (97 <= code && code <= 122);
  }

  ui static bool IsBigLetter(string letter)
  {
    int code = letter.CharCodeAt(0);
    return (65 <= code && code <= 90);
  }

  ui string GetTargetFlags(Actor target)
  {
    string result = "";

    if (target.bFRIENDLY && !target.player)
      {
        result = "Friendly";
      }

    if (target.bINVULNERABLE)
      {
        if (result.Length() > 0) { result.AppendFormat(" "); }
        result.AppendFormat("Invulnerable");
      }

    if (target.bBOSS)
      {
        if (result.Length() > 0) { result.AppendFormat(" "); }
        result.AppendFormat("Boss");
      }

    return result;
  }

} // class m8f_ts_EventHandler

class m8f_ts_TagCache
{
  string cachedClass;
  string cachedTag;

  void SetCache(string targetClass, string targetTag)
  {
    cachedClass = targetClass;
    cachedTag   = targetTag;
  }

  m8f_ts_TagCache Init()
  {
    cachedClass = "";
    cachedTag   = "";
    return self;
  }
}

class m8f_ts_PlayToUiTranslator
{
  play Actor AimTargetWrapper(Actor a) const
  {
    return a.AimTarget();
  }

  play Actor LineAttackTargetWrapper(Actor a) const
  {
    FLineTraceData lineTraceData;
    a.LineTrace(a.angle, 4000.0, a.pitch, 0, 0.0, 0.0, 0.0, lineTraceData);
    return lineTraceData.HitActor;
  }

  play Actor AimLineAttackWrapper(Actor a) const
  {
    FTranslatedLineTarget ftlt;
    a.AimLineAttack(a.angle, 2048.0, ftlt, 0,
                    ALF_CHECKNONSHOOTABLE | ALF_FORCENOSMART);
    return ftlt.linetarget;
  }
}
